plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
}

group = 'io.github.leeseojune53'
version = '1.0.3'
sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'neis-api'
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Neis Api'
                description = 'Supporting to develop KOREA school service'
                url = 'https://github.com/leeseojune53/neis-api'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'leeseojune'
                        name = 'Seojune Lee'
                        email = 'sung07288346@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:svn:http://github.com/'
                    developerConnection = 'scm:svn:https://github.com/'
                    url = 'http://github.com/'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/leeseojune53/neis-api"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

signing {
    useGpgCmd()
    sign(publishing.publications)
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}